namespace PortPolicies {
	policy access_internal_parking {
		target
			clause Attributes.actionId == "http://drozdowicz.net/onto/access-control-action#Open" 
				and SxacmlAttributes.resourceClassId == "http://drozdowicz.net/onto/port#BarrierController":anyURI
		apply permitUnlessDeny
		
		rule {
			deny
			condition not (anyURIIsIn("http://drozdowicz.net/onto/port#ObservedUser":anyURI, SxacmlAttributes.subjectClassId))
		} 
		
		
		rule {
			deny
			condition not (anyURIIsIn("http://drozdowicz.net/onto/port_mapping#DriverAuthorizedForTruck":anyURI, SxacmlAttributes.subjectClassId))			
		}				
		
		rule {
			deny
			condition not (
				dateTimeFromString(stringOneAndOnly(Sxacml.sparqlPathSelect("?subject port:drivesTruck/port:hasPlannedDelivery/logiserv:hasTime/time:hasBeginning/time:inXsdTime"))) 
					>= dateTimeSubtractDayTimeDuration(dateTimeOneAndOnly(Attributes.currentDateTime), "PT1H":dayTimeDuration)
				&& dateTimeFromString(stringOneAndOnly(Sxacml.sparqlPathSelect("?subject port:drivesTruck/port:hasPlannedDelivery/logiserv:hasTime/time:hasEnd/time:inXsdTime"))) 
					< dateTimeAddDayTimeDuration(dateTimeOneAndOnly(Attributes.currentDateTime), "PT1H":dayTimeDuration))
			
		}
		
		
		rule {
			deny
			condition not (
				stringBagSize(Sxacml.sparqlPathSelect("?resource port:isHostedBy")) > 0 
				&&				
				stringBagSize(Sxacml.sparqlPathSelect("?environment sosa:hasObservation/sosa:madeBySensor/port:isHostedBy")) > 0
				&&
				allOf(
					function [stringEqual],
					stringOneAndOnly(Sxacml.sparqlPathSelect("?resource port:isHostedBy")),
					Sxacml.sparqlPathSelect("?environment sosa:hasObservation/sosa:madeBySensor/port:isHostedBy")
					)
				)
		}
	}
}
